100 rem *** MUGWUMP - A HIDE AND SEEK GAME
110 rem *** PEOPLE'S COMPUTER COMPANY, MENLO PARK CA
120 randomize timer
130 rem *** G=GRID SIZE  N=NUMBER OF GUESSES ALLOWED
140 g=10
150 n=5
160 print "DO YOU WANT THE RULES (1=YES  0=NO)";
170 input z
180 if z<>1 then goto 350
190 rem *** RULES IN LINES 200 THRU 330
200 print "A MUGWUMP IS HIDING IN A";g;" BY";g;" GRID. TRY TO"
210 print "FIND HIM BY GUESSING HIS GRIDPOINT. HOMEBASE IS"
220 print "GRIDPOINT 0,0  AND A GUESS IS A PAIR OF WHOLE"
230 print "NUMBERS (0 TO";g-1;") SEPARATED BY A COMMA. THE FIRST"
240 print "NUMBER IS THE DISTANCE TO THE RIGHT OF HOMEBASE"
250 print "AND THE SECOND NUMBER IS THE DISTANCE ABOVE THE"
260 print "HOMEBASE. FOR EXAMPLE, IF YOU THINK THE MUGWUMP"
270 print "IS HIDING 8 UNITS TO THE RIGHT OF HOMEBASE AND"
280 print "3 UNITS ABOVE HOMEBASE, THEN ENTER  8,3  AS YOUR"
290 print "GUESS AND PRESS THE 'RETURN' KEY."
300 print
310 print "YOU GET";n;" GUESSES. AFTER EACH GUESS, I WILL"
320 print "TELL YOU HOW FAR (IN A DIRECT LINE) YOU ARE FROM"
330 print "THE MUGWUMP."
340 rem *** HIDE MUGWUMP AT RANDOM GRIDPOINT  A,B
350 a=int(g*rnd)
360 b=int(g*rnd)
370 print
380 print "MUGWUMP IS HIDING. YOU GET";n;" GUESSES."
390 rem *** N GUESSES ALLOWED - LINES 400 THRU 560
400 for t=1 to n
410 print
420 print "WHAT IS YOUR GUESS";
430 input x,y
440 rem *** IF MUGWUMP NOT FOUND GO TO LINE 500
450 if x<>a then goto 520
460 if y<>b then goto 520
470 print "YOU FOUND HIM IN";t;" GUESSES!!!"
480 print "LET'S PLAY AGAIN."
490 print
500 goto 350
510 rem *** D=STRAIGHTLINE DISTANCE TO MUGWUMP
520 d=sqr((x-a)^2+(y-b)^2)
530 rem *** THEN WE ROUND D TO ONE DECIMAL PLACE
540 d=int(10*d)/10
550 print "YOU ARE";d;" UNITS FROM THE MUGWUMP."
560 next t
570 rem *** MUGWUMP NOT FOUND IN N GUESSES
580 print
590 print "SORRY, THAT'S";n;" TRIES."
600 print "MUGWUMP IS AT GRIDPOINT ";a;",";b
610 print "LET'S PLAY AGAIN."
620 print
630 goto 350
640 end
