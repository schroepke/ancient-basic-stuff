100 rem DESIGNED AND PROGRAMMED BY CHRIS GAYLO, SYOSSET H.S.--9/12/70
110 print "                      H I G H  N O O N"
120 print "                      ----------------"
130 print
140 print
150 print "DO YOU WANT INSTRUCTIONS";
160 input d$
170 if d$="NO" then goto 460
180 print
190 print
200 print "YOU HAVE BEEN CHALLENGED TO A SHOWDOWN BY BLACK BART, ONE OF"
210 print "THE MEANEST DESPERADOES WEST OF THE ALLEGHENY MOUNTAINS."
220 print "WHILE YOU ARE WALKING DOWN A DUSTY, DESERTED SIDE STREET,"
230 print "BLACK BART EMERGES FROM A SALOON ONE HUNDRED PACES AWAY. BY"
240 print "AGREEMENT, YOU EACH HAVE FOUR CARTRIDGES IN YOUR SIX-GUNS."
250 print "YOUR MARKSMANSHIP EQUALS HIS. AT THE START OF THE WALK, NEI-"
260 print "THER OF YOU CAN POSSIBLY HIT THE OTHER, AND AT THE END OF"
270 print "THE WALK, NEITHER CAN MISS. THE CLOSER YOU GET, THE BETTER"
280 print "YOUR CHANCES OF HITTING BART, BUT HE ALSO HAS BETTER CHANCES"
290 print "OF HITTING YOU."
300 print "DO YOU STILL WANT TO CONTINUE";
310 input a$
320 if a$="NO" then goto 1370
330 print "THE MOVES ARE AS FOLLOWS:"
340 print
350 print
360 print "                        *M O V E S*"
370 print "                        ==========="
380 print
390 print "                        1. ADVANCE"
400 print 
410 print "2. STAND STILL                               3. FIRE"
420 print
430 print "4. JUMP BEHIND THE WATERING TROUGH           5. GIVE UP"
440 print
450 print "                        6. TURN TAIL AND RUN"
455 print
456 print
460 print "WHAT IS YOUR LUCKY NUMBER FOR TODAY";
470 input a
480 a=1e4*abs(sin(a))
490 a=(a-int(a))*1000
495 randomize
500 for i=1 to a
510 b=rnd(0)
520 next i
530 print
540 x=100
550 print
560 print "WHAT IS YOUR STRATEGY";
570 input b
580 b=int(b)
590 if b>6 then goto 1340
600 if b<1 then goto 1340
610 select case b
    case 1 
      goto 620
    case 2
      goto 1390
    case 3
      goto 690
    case 4
      goto 1410
    case 5
      goto 1510
    case 6
      goto 1610
    end select
620 print "HOW MANY PACES DO YOU ADVANCE";
630 input s
640 if s<0 then goto 2000
650 if s>10 then goto 1980
660 x=x-s
670 print "YOU ARE NOW";x;" PACES APART."
680 goto 1040
690 c=c+1
700 if c<=4 then goto 750
710 print "NICE GOING, ACE, YOU'VE RUN OUT OF SHELLS."
720 print "NOW BART WON'T SHOOT UNTIL YOU TOUCH NOSES."
730 print "YOU BETTER THINK OF SOMETHING FAST. (LIKE RUN)"
740 goto 1040
750 w=rnd*10
760 if w>x/10 then goto 820
770 if c=2 then goto 1820
780 if c=3 then goto 1840
790 print "WHAT A LOUSY SHOT."
800 if c=4 then goto 710
810 goto 1460
820 print "WHAT A SHOT, YOU GOT BLACK BART RIGHT BETWEEN THE EYES."
830 print
840 print
850 print "AS MAYOR OF DODGE CITY, AND ON BEHALF OF ITS CITIZENS,"
860 print "I EXTEND TO YOU OUR THANKS, AND PRESENT YOU WITH THIS"
870 print "REWARD, A CHECK FOR $20,000, FOR KILLING BLACK BART."
880 print
890 print "**********************************************************"
900 print
910 print "CHECK NO.";int(1000*rnd(-1));"                    AUG.";int(10+10*rnd);"TH. 1889"
920 print "            CASHIER'S RECEIT---BANK OF DODGE CITY"
930 print 
940 print "                 PAY TO THE BEARER ON DEMAND"
950 print
960 print "                         THE SUM OF"
970 print
980 print "      TWENTY THOUSAND DOLLARS--------------------$20,000"
990 print
1000 print "**********************************************************"
1010 print
1020 print "DON'T SPEND IT ALL IN ONE PLACE."
1030 goto 2080
1040 if x<10 then goto 1120
1050 q=rnd*10
1060 if q>5 then goto 1120
1070 z=int(2+rnd*9)
1080 print "BLACK BART MOVES ";z;" PACES"
1090 x=x-z
1100 print "YOU ARE NOW ";x;" PACES APART"
1110 goto 550
1120 p=p+1
1130 if c>4 then goto 1070
1140 if p<=4 then goto 1200
1150 if p=5 then goto 1180
1160 if p>6 then goto 2020
1170 goto 1070
1180 print "NOW IS YOUR CHANCE, BART IS OUT OF SHELLS"
1190 goto 1070
1200 r=rnd(-1)*10
1210 print "BART FIRES . . . . . ."
1220 if r>x/10 then goto 1300
1230 print "A MISS . . . . "
1240 if p=2 then goto 1890
1250 if p=3 then goto 1940
1260 if p>4 then goto 1070
1270 if p=4 then goto 1960
1280 print "WHEW, WERE YOU LUCKY. THAT BULLET JUST MISSED YOUR HEAD."
1290 goto 1490
1300 if b=4 then goto 1910
1310 print "BART SHOT YOU RIGHT THROUGH THE HEART THAT TIME."
1320 print "YOU WENT KICKIN' WITH YOUR BOOTS ON."
1330 goto 2080
1340 print "YOU SURE AREN'T GOING TO LIVE VERY LONG IF YOU CAN'T EVEN"
1350 print "FOLLOW DIRECTIONS"
1360 goto 550
1370 print "GREENHORN."
1380 goto 2080
1390 print "THAT MOVE MADE YOU A PERFECT STATIONARY TARGET"
1400 goto 1040
1410 t=t+1
1420 if t>3 then goto 2060
1430 if t>3 then goto 2060
1440 print "NOT A BAD MANEUVER, YOU THREW BART'S STRATEGY OFF"
1450 goto 1040
1460 if p>4 then goto 1050
1470 print "YOU NOW HAVE ";4-c;" SHELLS TO BART'S ";4-p;" SHELLS."
1480 goto 1040
1490 print "YOU NOW HAVE ";4-c;" SHELLS TO BART'S ";4-p;" SHELLS."
1500 goto 550
1510 print "BLACK BART ACCEPTS. THE CONDITIONS ARE THAT HE WON'T SHOOT YOU"
1520 print "IF YOU TAKE THE FIRST STAGE OUT OF TOWN AND NEVER COME BACK"
1530 print "AGREED";
1540 input h$
1550 if h$="NO" then goto 1590
1570 print "A VERY WISE DECISION."
1580 goto 2080
1590 print "OH WELL, BACK TO THE SHOWDOWN"
1600 goto 550
1610 print "HOW FAR DID YOU RUN";
1620 input f
1630 if f<50 then goto 1670
1640 print "MAN, DID HE RUN. HE RAN SO FAST EVEN THE DOGS COULDN'T"
1650 print "CATCH HIM"
1660 goto 2080
1670 if p>=4 then goto 1770
1680 print "BLACK BART FIRES ";4-p;" SHELLS......."
1690 if 4-p>1 then goto 1730
1700 print "HE GOT YOU RIGHT IN THE BACK- THATS WHAT YOU DESERVE"
1710 print "FOR RUNNING."
1720 goto 2080
1730 print "BLACK BART UNLOADED HIS GUN, ONCE IN YOUR BACK"
1740 print "AND ";3-p;" TIMES IN YOUR A**. NOW YOU CAN'T EVEN REST IN"
1750 print "PEACE."
1760 goto 2080
1770 print "YOU WERE LUCKY, BART CAN ONLY THROW HIS GUN AT YOU, HE"
1780 print "DOESN'T HAVE ANY SHELLS LEFT. YOU SHOULD REALLY BE DEAD."
1790 goto 2080
1800 if p=2 then goto 1940
1810 if p=3 then goto 1960
1820 print "GRAZED BART IN THE RIGHT ARM"
1830 goto 1040
1840 print "HE'S HIT IN THE LEFT SHOULDER, FORCING HIM TO USE HIS RIGHT"
1850 print "HAND TO SHOOT WITH"
1860 goto 1040
1870 print "THAT WAS YOUR LAST SHOT, YOU MISSED"
1880 goto 1040
1890 print "BUT BART GOT YOU IN THE RIGHT SHIN."
1900 goto 550
1910 print "THAT TRICK JUST SAVED YOUR LIFE. BART'S BULLET"
1920 print "WAS STOPPED BY THE WOOD SIDES OF THE TROUGH:"
1930 goto 550
1940 print "THOUGH BART GOT YOU ON THE LEFT SIDE OF YOUR JAW."
1950 goto 550
1960 print "BART MUST HAVE JERKED THE TRIGGER"
1970 goto 550
1980 print "NOBODY CAN WALK THAT FAST"
1990 goto 620
2000 print "NONE OF THIS NEGATIVE STUFF PARTNER, ONLY POSITIVE NUMBERS"
2010 goto 620
2020 print "BART JUST HI-TAILED IT OUT OF TOWN RATHER THAN FACE YOU WITH-"
2030 print "OUT A LOADED GUN. YOU CAN REST ASSURED THAT BART WON'T EVER"
2040 print "SHOW HIS FACE AROUND THIS TOWN AGAIN."
2050 goto 2080
2060 print "HOW MANY WATERING TROUGHS DO YOU THINK ARE ON THIS STREET"
2070 goto 550
2080 print
2090 print "C.G. INC."
2100 rem THE VARIABLE USAGE IS AS FOLLOWS:
2110 rem     d$--INSTRUCTION NEEDED
2120 rem     a$--INTENT TO CONTINUE THE GAME AFTER RECEIVING INSTRUCTIONS
2130 rem     a---STORES YOUR LUCKY NUMBER
2140 rem     i---USED FOR THE RANDOM NUMBER GENERATOR
2150 rem     x---DENOTES THE DISTANCE BETWEEN YOU
2160 rem     b---DENOTES THE NUMBER OF YOUR MOVE
2170 rem     s---THE NUMBER OF PACES YOU ADVANCE
2180 rem     c---COUNTER FOR THE NUMBER OF YOUR SHELLS
2190 rem     w---RANDOM NUMBER FOR THE HITTING OF BART
2200 rem     q---RANDOM NUMBER FOR BART'S CHOICE OF WALK OR FIRE
2210 rem     z---RANDOM NUMB W!DH"! THE PACES BART ADVANCES
2220 rem     p---COUNTER FOR BART'S REMAINING SHELLS
2230 rem     r---RANDOM NUMBER FOR BART HITTING YOU
2240 rem     t---COUNTER FOR THE NUMBER OF WATERING TROUGHS
2250 rem     f---DISTANCE YOU RAN
2260 rem     h$--DECISION ON GIVING UP
2270 end

